<!doctype html>
<html>

<head>
  <meta charset="utf-8">
  <script src="../libraries/hamster.min.js"></script>
  <title>Hyperbolic tiling</title>
  <style>
    html, body { 
      margin: 0; 
    }
    canvas {
      display: block;
    }
  </style>
</head>
<script src="../libraries/pixi.min.js"></script>

<body>

  <script type="text/javascript">
    let type = "WebGL";
    if(!PIXI.utils.isWebGLSupported()){
      type = "canvas";
    }
    PIXI.utils.sayHello(type);
  </script>

  <div id="container"></div>

  <script id="fragmentShader" type="x-shader/x-fragment">
    uniform vec2 iResolution;
    uniform float xmin;
    uniform float xmax;
    uniform float ymin;
    uniform float ymax;
    uniform float iGlobalTime;
    uniform bool anim;
    uniform float time;

    struct torusTransfo{
      mat4 transfo;
      float radius;
    };

    mat3 inverse(mat3 m) {
      float a00 = m[0][0], a01 = m[0][1], a02 = m[0][2];
      float a10 = m[1][0], a11 = m[1][1], a12 = m[1][2];
      float a20 = m[2][0], a21 = m[2][1], a22 = m[2][2];

      float b01 = a22 * a11 - a12 * a21;
      float b11 = -a22 * a10 + a12 * a20;
      float b21 = a21 * a10 - a11 * a20;

      float det = a00 * b01 + a01 * b11 + a02 * b21;

      return mat3(b01, (-a22 * a01 + a02 * a21), (a12 * a01 - a02 * a11),
                  b11, (a22 * a00 - a02 * a20), (-a12 * a00 + a02 * a10),
                  b21, (-a21 * a00 + a01 * a20), (a11 * a00 - a01 * a10)) / det;
    }

    mat4 inverse(mat4 m) {
      float
        a00 = m[0][0], a01 = m[0][1], a02 = m[0][2], a03 = m[0][3],
        a10 = m[1][0], a11 = m[1][1], a12 = m[1][2], a13 = m[1][3],
        a20 = m[2][0], a21 = m[2][1], a22 = m[2][2], a23 = m[2][3],
        a30 = m[3][0], a31 = m[3][1], a32 = m[3][2], a33 = m[3][3],

        b00 = a00 * a11 - a01 * a10,
        b01 = a00 * a12 - a02 * a10,
        b02 = a00 * a13 - a03 * a10,
        b03 = a01 * a12 - a02 * a11,
        b04 = a01 * a13 - a03 * a11,
        b05 = a02 * a13 - a03 * a12,
        b06 = a20 * a31 - a21 * a30,
        b07 = a20 * a32 - a22 * a30,
        b08 = a20 * a33 - a23 * a30,
        b09 = a21 * a32 - a22 * a31,
        b10 = a21 * a33 - a23 * a31,
        b11 = a22 * a33 - a23 * a32,

        det = b00 * b11 - b01 * b10 + b02 * b09 + b03 * b08 - b04 * b07 + b05 * b06;

      return mat4(
        a11 * b11 - a12 * b10 + a13 * b09,
        a02 * b10 - a01 * b11 - a03 * b09,
        a31 * b05 - a32 * b04 + a33 * b03,
        a22 * b04 - a21 * b05 - a23 * b03,
        a12 * b08 - a10 * b11 - a13 * b07,
        a00 * b11 - a02 * b08 + a03 * b07,
        a32 * b02 - a30 * b05 - a33 * b01,
        a20 * b05 - a22 * b02 + a23 * b01,
        a10 * b10 - a11 * b08 + a13 * b06,
        a01 * b08 - a00 * b10 - a03 * b06,
        a30 * b04 - a31 * b02 + a33 * b00,
        a21 * b02 - a20 * b04 - a23 * b00,
        a11 * b07 - a10 * b09 - a12 * b06,
        a00 * b09 - a01 * b07 + a02 * b06,
        a31 * b01 - a30 * b03 - a32 * b00,
        a20 * b03 - a21 * b01 + a22 * b00) / det;
    }

    mat3 transpose(mat3 m) {
      return mat3(m[0][0], m[1][0], m[2][0],
                  m[0][1], m[1][1], m[2][1],
                  m[0][2], m[1][2], m[2][2]);
    }

    mat4 inverseTransfoMatrix(mat4 M){
      mat3 B = transpose(mat3(M)); // this is the inverse of the upper left block
      vec3 v = B * M[3].xyz;
      return mat4(vec4(B[0],0.0),vec4(B[1],0.0),vec4(B[2],0.0), vec4(-v, 1.0));
    }
 
    mat3 Rx(float phi){
      return mat3(1.0, 0.0, 0.0, 0.0, cos(phi), -sin(phi), 0.0, sin(phi), cos(phi));
    }

    mat3 Ry(float phi){
      return mat3(cos(phi), 0.0, sin(phi), 0.0, 1.0, 0.0, -sin(phi), 0.0, cos(phi));
    }

    mat3 Rz(float phi){
      return mat3(cos(phi), -sin(phi), 0.0, sin(phi), cos(phi), 0.0, 0.0, 0.0, 1.0);
    }

    vec3 rotate(vec3 pt, float ax, float ay, float az){
      return Rx(ax) * (Ry(ay) * (Rz(az) * pt));
    }

    torusTransfo T(vec3 p1, vec3 p2, vec3 p3){
      vec3 p12 = (p1+p2)/2.0, p23 = (p2+p3)/2.0;
      vec3 coeffs = vec3(
        (p1.y-p2.y)*(p2.z-p3.z)-(p1.z-p2.z)*(p2.y-p3.y),
        (p1.z-p2.z)*(p2.x-p3.x)-(p1.x-p2.x)*(p2.z-p3.z),
        (p1.x-p2.x)*(p2.y-p3.y)-(p1.y-p2.y)*(p2.x-p3.x)
      );
      float offset1 = dot(p1, coeffs);
      vec3 v12 = p2-p1, v23 = p3-p2;
      float offset21 = dot(p12,v12), offset22 = dot(p23,v23);
      mat3 A = transpose(mat3(coeffs, v12, v23));
      // center of circle passing by p1,p2,p3
      vec3 center = inverse(A) * vec3(offset1,offset21,offset22);
      // radius
      float radius = length(p1-center);
      //
      if(coeffs.x==0.0 && coeffs.y==0.0){
        mat4 t = mat4(vec4(1.0,0.0,0.0,0.0),vec4(0.0,1.0,0.0,0.0),vec4(0.0,0.0,1.0,0.0),vec4(-center,1.0));
        return torusTransfo(t, radius);
      }
      //
      vec3 n = normalize(coeffs);
      float s = sqrt(n.x*n.x+n.y*n.y);
      vec3 u = vec3(n.y/s, -n.x/s, 0.0);
      vec3 v = cross(n,u);
      mat4 t = mat4(vec4(u,0.0), vec4(v,0.0), vec4(n,0.0), vec4(center,1.0));
      return torusTransfo(inverseTransfoMatrix(t), radius);
    }

    // define a tetrahedron
    const float phi = (1.0+sqrt(5.0))/2.0;
    const float a = 1.0/sqrt(3.0);
    const float b = a/phi;
    const float c = a*phi;
    const vec3 A = vec3(a,  a, -a);
    const vec3 B = vec3(-c,  0.0, -b);
    const vec3 C = vec3(b, -c,  0.0);
    const vec3 D = vec3(0.0,  b,  c);
    const float r = 0.1;

    // signed distance
    float f(vec3 pt, torusTransfo tr1, torusTransfo tr2, torusTransfo tr3, torusTransfo tr4){
      float R1 = tr1.radius, R2 = tr2.radius, R3 = tr3.radius, R4 = tr4.radius;
      pt = rotate(pt, iGlobalTime, iGlobalTime, 0.0);
      vec3 pt1 = (tr1.transfo * vec4(pt, 1.0)).xyz;
      float d1 = length(vec2(length(pt1.xy) - R1, pt1.z)) - r;
      vec3 pt2 = (tr2.transfo * vec4(pt, 1.0)).xyz;
      float d2 = length(vec2(length(pt2.xy) - R2, pt2.z)) - r;
      vec3 pt3 = (tr3.transfo * vec4(pt, 1.0)).xyz;
      float d3 = length(vec2(length(pt3.xy) - R3, pt3.z)) - r;
      vec3 pt4 = (tr4.transfo * vec4(pt, 1.0)).xyz;
      float d4 = length(vec2(length(pt4.xy) - R4, pt4.z)) - r;
      return min(min(min(d1,d2),d3),d4);
    }

    // ro is the ray origin, rd is the ray direction
    float getT( vec3 ro, vec3 rd ) {
      torusTransfo tr1 = T(A, B, C);
      torusTransfo tr2 = T(A, B, D);
      torusTransfo tr3 = T(A, C, D);
      torusTransfo tr4 = T(B, C, D);
      float t = 0.0; 
      float d;
      vec3 r;
      for(int i = 0; i < 100; i++){
        r = ro + t*rd;
        d = f(r, tr1, tr2, tr3, tr4);
        if(d < 0.001){
          return t;
        }
        t += d;
      }
      return 0.0;
    }

    vec3 normal(vec3 r, float eps){
      torusTransfo tr1 = T(A, B, C);
      torusTransfo tr2 = T(A, B, D);
      torusTransfo tr3 = T(A, C, D);
      torusTransfo tr4 = T(B, C, D);
      float outx = 
        f(vec3(r.x+eps,r.y,r.z), tr1, tr2, tr3, tr4) - 
        f(vec3(r.x-eps,r.y,r.z), tr1, tr2, tr3, tr4);
      float outy = 
        f(vec3(r.x,r.y+eps,r.z), tr1, tr2, tr3, tr4) - 
        f(vec3(r.x,r.y-eps,r.z), tr1, tr2, tr3, tr4);
      float outz = 
        f(vec3(r.x,r.y,r.z+eps), tr1, tr2, tr3, tr4) - 
        f(vec3(r.x,r.y,r.z-eps), tr1, tr2, tr3, tr4);
      return normalize(vec3(outx, outy, outz));
    }


    vec3 sphereColor = vec3(0.5, 0.5, 0.0);
    
    void main(void){
      float aspectRatio = iResolution.x/iResolution.y;
      vec2 uv = vec2(
        ((xmax - xmin) * gl_FragCoord.x/iResolution.x + xmin) * aspectRatio, 
        (ymax - ymin) * gl_FragCoord.y/iResolution.y + ymin
      );

      // Cast a ray out from the eye position into the scene
      vec3 ro = vec3(uv.x, uv.y, 12.0); // Eye position is slightly up and back from the scene origin
      // Ray we cast is tilted slightly downward to give a better view of the scene
      vec3 rd = normalize(vec3(-0.0 + uv, -11.0));

      // Default color if we don't intersect with anything
      vec3 rayColor = vec3(0.2, 0.2, 0.2);
      // Direction the lighting is coming from
      vec3 lightDir = normalize(vec3(0, 0, 0.5));
      // Ambient light color
      vec3 ambient = vec3(0.05, 0.1, 0.1);
      // See if the ray intesects with any objects.
      // Provides the normal of the nearest intersection point and color
      float t = getT(ro, rd);
      if ( t < 0.0 || t > 0.0 ) {
        vec3 nrml = normal(ro + t*rd, 0.01);

        vec3 toEye = -rd;
        vec3 r_m = -reflect(lightDir, nrml);
        float spec    = 0.4 * 0.8 * pow(max(dot(r_m, toEye), 0.0), 8.0);

        float diffuse = clamp(dot(nrml, lightDir), 0.0, 1.0); // diffuse factor
        rayColor = sphereColor * (diffuse + ambient) + spec;
      }
      gl_FragColor = vec4(rayColor, 1.0);
    }
  </script>

  <script>

    let xmin = -1.2, xmax = 1.2, ymin = -1.2, ymax = 1.2; 

    var app = new PIXI.Application({ width: window.innerWidth, height: window.innerHeight, antialiasing: false });
    app.resizeTo = window;

    let fragmentShader = document.getElementById('fragmentShader').textContent;
    let filter = new PIXI.Filter(null, fragmentShader);
    filter.uniforms.iResolution = {x: app.screen.width, y: app.screen.height};
    filter.uniforms.xmin = xmin;
    filter.uniforms.xmax = xmax;
    filter.uniforms.ymin = ymin;
    filter.uniforms.ymax = ymax;
    filter.uniforms.iGlobalTime = 0;
    filter.uniforms.anim = false;
    filter.uniforms.time = -Math.PI/2;

    document.onclick = function(evt){
      filter.uniforms.anim = !filter.uniforms.anim;
      filter.uniforms.time = filter.uniforms.iGlobalTime;
    };

    let zoom0 = 1.001;
    let hamster = Hamster(document.body);
    hamster.wheel(function(event, delta, deltaX, deltaY){
      let ex = event.originalEvent.clientX;
      let ey = event.originalEvent.clientY;
      let zoom = Math.pow(zoom0, deltaY);
      let sx = (filter.uniforms.xmax - filter.uniforms.xmin) / filter.uniforms.iResolution.x;
      let sy = (filter.uniforms.ymax - filter.uniforms.ymin) / filter.uniforms.iResolution.y;
      let dx = ex - filter.uniforms.iResolution.x / 2;
      let dy = filter.uniforms.iResolution.y / 2 - ey;
      sx /= zoom;
      sy /= zoom;
      let midx = (filter.uniforms.xmin + filter.uniforms.xmax) / 2 + (zoom - 1) * dx * sx;
      let midy = (filter.uniforms.ymin + filter.uniforms.ymax) / 2 + (zoom - 1) * dy * sy;
      let rangex = (filter.uniforms.xmax - filter.uniforms.xmin) / zoom;
      let rangey = (filter.uniforms.ymax - filter.uniforms.ymin) / zoom;
      filter.uniforms.xmax = (rangex + 2 * midx) / 2;
      filter.uniforms.ymax = (rangey + 2 * midy) / 2;
      filter.uniforms.xmin = filter.uniforms.xmax - rangex;
      filter.uniforms.ymin = filter.uniforms.ymax - rangey;
    });

    let container = new PIXI.Container();
    container.filterArea = app.screen;
    container.filters = [filter];
    app.stage.addChild(container);
    
    document.getElementById('container').appendChild(app.view);

    function onresize(event){
      if(app.resize)
        app.resize();
      container.filterArea = app.screen;
      filter.uniforms.iResolution = {x: app.screen.width, y: app.screen.height};
    }
    window.addEventListener('resize', onresize, false);

    let startTime = Date.now();
    app.ticker.add(function(delta){
      var currentTime = Date.now();
      filter.uniforms.iGlobalTime = (currentTime - startTime) * 0.0005;
    });

  </script>
</body>

</html>
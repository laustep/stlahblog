<!doctype html>
<html>

<head>
  <meta charset="utf-8">
  <title>Orbit trap</title>
  <style>
    html, body { 
      margin: 0; 
    }
    canvas {
      display: block;
    }
  </style>
</head>
<script src="../libraries/pixi.min.js"></script>

<body>

  <script type="text/javascript">
    let type = "WebGL";
    if (!PIXI.utils.isWebGLSupported()) {
      type = "canvas";
    }
    PIXI.utils.sayHello(type);
  </script>

  <div id="container"></div>

  <script id="fragmentShader" type="x-shader/x-fragment">
    precision highp float;
    uniform vec2 iResolution;
    uniform vec2 mouse;
    uniform float iGlobalTime;

    // map function: map(x a, b, c, d) sends (a,b) to (b,c)
    float lerp(float t, float a, float b){
      return a + t*(b-a);
    }
    float norm(float t, float a, float b){
      return (t-a)/(b-a);
    }
    float map(float t, float e1, float s1, float e2, float s2){
      return lerp(norm(t, e1, s1), e2, s2);
    }

    // color mapping
    vec3 hsv2rgb(vec3 c){
      vec4 K = vec4(1.0, 2.0/3.0, 1.0/3.0, 3.0);
      vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
      return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
    }
    float pi = 3.1415926535897932384626433832795;

    // Mobius transformation
    vec2 product(vec2 a, vec2 b){
      return vec2(a.x*b.x-a.y*b.y, a.x*b.y+a.y*b.x);
    }
    vec2 conjugate(vec2 a){
      return vec2(a.x, -a.y);
    }
    vec2 divide(vec2 a, vec2 b){
      return vec2(a.x*b.x+a.y*b.y, a.y*b.x-a.x*b.y) / (b.x*b.x+b.y*b.y);
    }
    vec2 mobius(vec2 z, vec2 gamma, float t){
      float g2 = gamma.x*gamma.x + gamma.y*gamma.y;
      float h = sqrt(1.0 - g2);
      vec2 d2 = pow(h, t) * vec2(cos(t*pi/2.0), sin(t*pi/2.0));
      vec2 d1 = conjugate(d2);
      vec2 a = vec2(d1.x, -d1.y/h);
      vec2 b = d2.y * gamma / h;
      vec2 c = conjugate(b);
      vec2 d = conjugate(a);
      return divide(product(a, z) + b, product(c, z) + d);
    }

    // 
    float f(vec2 z) {
      return min(abs(z.x), abs(z.y)) / 0.03;
    }
    vec2 orbitTrap(vec2 zc) {
      vec2 z = vec2(0.0, 0.0);
      for(int i = 0; i < 100; i += 1) {
        z = conjugate(product(z, z)) + zc;
        if((length(z) > 100.0) || (i >= 0 && f(z) < 1.0)) {
          return z;
        }
      }
      return z;
    }

    vec2 g(vec2 z, vec2 gamma, float t) {
      return orbitTrap(mobius(z, gamma, t));
    }

    vec3 pair2hsv(vec2 z){
      float h = (atan(z.y, z.x) + pi) / 2.0 / pi;
      float fz = 2.0*f(z);
      float s = min(1.0, fz);
      float v = max(0.0, min(1.0, 2.0 - fz));
      return vec3(h, s, v);
    }

    //
    void main(void){
      float t = (1.0 + cos(iGlobalTime));
      float gammax = map(mouse.x, 0.0, iResolution.x, -0.7, 0.7); 
      float gammay = map(mouse.y, 0.0, iResolution.y, -0.7, 0.7);
      float r = iResolution.x / iResolution.y;
      float x = map(gl_FragCoord.x, 0.0, iResolution.x, -2.0*r, 1.0*r); 
      float y = map(gl_FragCoord.y, 0.0, iResolution.y, -1.5, 1.5);
      vec2 z = g(vec2(x, y), vec2(gammax, gammay), t);
      gl_FragColor = vec4(hsv2rgb(pair2hsv(z)), 1.0);
    }
  </script>

  <script>
    var app = new PIXI.Application({ width: window.innerWidth, height: window.innerHeight });
    app.resizeTo = window;

    let fragmentShader = document.getElementById('fragmentShader').textContent;

    let filter = new PIXI.Filter(null, fragmentShader);
    filter.uniforms.iResolution = [app.screen.width, app.screen.height];
    filter.uniforms.mouse = {x: app.screen.width/2, y: app.screen.height/2}; 
    filter.uniforms.iGlobalTime = 0.0;

    document.onmousemove = function(evt){
      filter.uniforms.mouse = {x: evt.clientX, y: evt.clientY};
    };

    let container = new PIXI.Container();
    container.filterArea = app.screen;
    container.filters = [filter];

    app.stage.addChild(container);
    document.getElementById('container').appendChild(app.view);

    function onresize(event){
      if(app.resize)
        app.resize();
      container.filterArea = app.screen;
      filter.uniforms.iResolution = [app.screen.width, app.screen.height];
    }
    window.addEventListener('resize', onresize, false);

    startTime = Date.now();
    app.ticker.add(function(delta){
      var currentTime = Date.now();
      filter.uniforms.iGlobalTime = (currentTime - startTime) * 0.0005;
    });

  </script>
</body>

</html>